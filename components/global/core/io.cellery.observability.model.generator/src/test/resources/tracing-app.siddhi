@App:name("model-test-tracing-app")
define stream ZipkinStreamIn(traceId string, id string, parentId string, name string, serviceName string,
                             kind string, timestamp long, duration long, tags string);

define function extractFromServiceName[javascript] return string {
    var serviceName = data[0];
    var index = data[1];    // 1: instance, 2: service, 3: instanceKind, 4: namespace

    var cellGatewayMatches = /^([a-z0-9]+(?:-[a-z0-9]+)*)--([a-z0-9]+(?:-[a-z0-9]+)*)\.(.+)$/.exec(serviceName);
    var mircoserviceMatches = /^([a-z0-9]+(?:-[a-z0-9]+)*)--([a-z0-9]+(?:-[a-z0-9]+)*)--([a-z0-9]+(?:-[a-z0-9]+)*)\.(.+)$/.exec(serviceName);

    var extractedData = "";
    if (cellGatewayMatches) {
        if (index == 3) {
            extractedData = "Cell";
        } else if (index == 4) {
            extractedData = cellGatewayMatches[3];
        } else {
            extractedData = cellGatewayMatches[index];
        }
    } else if (mircoserviceMatches) {
        extractedData = mircoserviceMatches[index];
        if (index == 3) {
            if (extractedData == "cell") {
                extractedData = "Cell";
            } else if (extractedData == "composite") {
                extractedData = "Composite";
            } else {
                extractedData = "";
            }
        }
    } else {
        extractedData = "";
    }

    // Handling service names that does not match any of the regexes
    if (!extractedData && index == 2) {
        extractedData = serviceName;
    }
    return extractedData.replace(/_/g, "-");
};

--
-- Storing Zipkin data received for Tracing
--

from ZipkinStreamIn
select
    traceId,
    id as spanId,
    parentId,
    extractFromServiceName(serviceName, 4) as namespace,
    extractFromServiceName(serviceName, 1) as instance,
    extractFromServiceName(serviceName, 3) as instanceKind,
    extractFromServiceName(serviceName, 2) as serviceName,
    "" as pod,
    name as operationName,
    kind,
    timestamp as startTime,
    duration,
    tags
insert into ProcessedZipkinStream;

from ProcessedZipkinStream#observe:traceGroupWindow(5 sec,traceId)#observe:modelGenerator(instance, instanceKind, serviceName, operationName, spanId, parentId, kind, traceId, startTime)
select *
insert into AfterModelGenStream;
